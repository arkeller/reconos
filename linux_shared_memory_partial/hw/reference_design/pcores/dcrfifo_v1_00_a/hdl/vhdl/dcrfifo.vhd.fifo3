----------------------------------------------------------------------------------
-- Company:  Uni Paderborn 
-- Engineer: Andreas Agne
-- 
-- Create Date:    06:26:33 02/15/2009 
-- Design Name: 
-- Module Name:    dcrfifo - Behavioral 
-- Project Name:   Diplomarbeit
-- Target Devices: Virtex2Pro
-- Tool versions: 
-- Description: 
--
-- Dependencies: 
--
-- Revision: 
-- Revision 0.01 - File Created
-- Additional Comments: 
--
----------------------------------------------------------------------------------
library IEEE;
use IEEE.STD_LOGIC_1164.ALL;
use IEEE.STD_LOGIC_ARITH.ALL;
use IEEE.STD_LOGIC_UNSIGNED.ALL;

---- Uncomment the following library declaration if instantiating
---- any Xilinx primitives in this code.
--library UNISIM;
--use UNISIM.VComponents.all;

entity dcrfifo is
	generic (
		C_DCR_BASEADDR :     std_logic_vector := "1111111111";
		C_DCR_HIGHADDR :     std_logic_vector := "0000000000";
		C_DCR_AWIDTH   :     integer          := 10;
		C_DCR_DWIDTH   :     integer          := 32;
		C_NUM_REGS     :     integer          := 2;
		C_FIFO_AWIDTH  :     integer          := 15
	);
	port ( -- the DCR bus interface
		clk            : in  std_logic;
		reset          : in  std_logic;             -- high active synchronous
		o_dcrAck       : out std_logic;
		o_dcrDBus      : out std_logic_vector(C_DCR_DWIDTH-1 downto 0);
		i_dcrABus      : in  std_logic_vector(C_DCR_AWIDTH-1 downto 0);
		i_dcrDBus      : in  std_logic_vector(C_DCR_DWIDTH-1 downto 0);
		i_dcrRead      : in  std_logic;
		i_dcrWrite     : in  std_logic
	);

end dcrfifo;

architecture Behavioral of dcrfifo is

	-- address of count (read only)
	constant ADDR_COUNT : std_logic_vector := C_DCR_BASEADDR;
	-- address of fifo (read/write)
	constant ADDR_FIFO  : std_logic_vector := C_DCR_BASEADDR + 1;
	-- fifo count width
	constant FIFO_DEPTH : integer := 2**C_FIFO_AWIDTH;	
	
	-- fifo memory type
	type t_ram is array (FIFO_DEPTH-1 downto 0) of std_logic_vector(C_DCR_DWIDTH-1 downto 0);
		
	-- fifo memory
	signal fifo_mem : t_ram;
	-- fifo output
	signal fifo_out : std_logic_vector(C_DCR_DWIDTH-1 downto 0);
	-- fifo input
	signal fifo_in : std_logic_vector(C_DCR_DWIDTH-1 downto 0);
	-- fifo output address register
	signal fifo_outaddr : std_logic_vector(C_FIFO_AWIDTH-1 downto 0);
	-- fifo input address register
	signal fifo_inaddr : std_logic_vector(C_FIFO_AWIDTH-1 downto 0);
	-- fifo count register
	signal fifo_count : std_logic_vector(C_FIFO_AWIDTH-1 downto 0);
	-- fifo write_enable signal
	signal fifo_we : std_logic;
	-- advance to next output word
	signal fifo_next : std_logic;
	-- fifo full indicator
	signal fifo_full : std_logic;
	-- fifo empty indicator
	signal fifo_empty : std_logic;
	-- fifo overflow
	signal fifo_overflow : std_logic;
	-- fifo underrun
	signal fifo_underrun : std_logic;
	-- fifo contol
	signal fifo_ctrl : std_logic;
	-- fifo write only
	signal fifo_wronly_set : std_logic;
	signal fifo_wronly : std_logic;
	-- fifo reset
	signal fifo_reset : std_logic;
	
	-- registers indicating type of request
	signal readCountReg  : std_logic;
	signal writeCountReg : std_logic; 
	signal readFifoReg   : std_logic;
	signal writeFifoReg  : std_logic;

	-- asynchronous signals indicating type of request (input to the registers above)
	signal readCount  : std_logic;
	signal writeCount : std_logic;
	signal readFifo   : std_logic;
	signal writeFifo  : std_logic;
begin
	-- asynchronously determine the type of request
	readCount  <= '1' when i_dcrRead  = '1' and i_dcrABus = ADDR_COUNT else '0';
	writeCount <= '1' when i_dcrWrite = '1' and i_dcrABus = ADDR_COUNT else '0';
	readFifo   <= '1' when i_dcrRead  = '1' and i_dcrABus = ADDR_FIFO  else '0';
	writeFifo  <= '1' when i_dcrWrite = '1' and i_dcrABus = ADDR_FIFO  else '0';
	-- synchronous ack
	o_dcrAck <= readCountReg or readFifoReg or writeFifoReg or writeCountReg;
	
	
	-- fifo next word
	fifo_next <= readFifoReg and (not readFifo);
	-- fifo write enable
	fifo_we   <= (not writeFifoReg) and writeFifo;
	-- control register access
	fifo_ctrl <= (not writeCountReg) and writeCount;
	-- connect fifo input to DCR
	fifo_in <= i_dcrDBus;
	-- fifo full and empty signals
	fifo_empty <= '1' when CONV_INTEGER(fifo_count) = 0 else '0';
	fifo_full  <= '1' when CONV_INTEGER(fifo_count) = FIFO_DEPTH - 1 else '0';
	--
	fifo_wronly_set <= '0' when i_dcrDBus = X"AFFEBEEF" else '1';
	fifo_reset  <= '1' when i_dcrDBus = X"AFFEDEAD" else '0';
	
	-- bypass mux as in UG018 page 105
	bypass_mux : process (readFifo, readCount, i_dcrDBus, fifo_count, fifo_out,
			fifo_underrun, fifo_overflow, fifo_wronly)
	begin
		o_dcrDBus <= i_dcrDBus;
		if    readFifo  = '1' then o_dcrDBus <= fifo_out;
		elsif readCount = '1' then
			o_dcrDBus(C_FIFO_AWIDTH-1 downto 0) <= fifo_count;
			o_dcrDBus(C_DCR_DWIDTH-1) <= fifo_underrun;
			o_dcrDBus(C_DCR_DWIDTH-2) <= fifo_overflow;
			o_dcrDBus(C_DCR_DWIDTH-7) <= fifo_wronly;
		end if;
	end process;
	
	-- process for synchronous generation of o_dcrAck related signals
	synchronous_ack : process (clk, reset, readFifo, readCount, writeFifo)
	begin
		if reset = '1' then
			readCountReg  <= '0';
			readFifoReg   <= '0';
			writeFifoReg  <= '0';
			writeCountReg <= '0';
		elsif rising_edge(clk) then
			readCountReg  <= '0';
			readFifoReg   <= '0';
			writeFifoReg  <= '0';
			writeCountReg <= '0';
			if readFifo   = '1' then readFifoReg   <= '1'; end if;
			if readCount  = '1' then readCountReg  <= '1'; end if;
			if writeFifo  = '1' then writeFifoReg  <= '1'; end if;
			if writeCount = '1' then writeCountReg <= '1'; end if;
		end if;
	end process;
	
	fifo : process (clk, reset, fifo_we, fifo_next, fifo_full, fifo_empty, fifo_reset,
			fifo_ctrl, fifo_wronly, fifo_wronly_set) 
	begin
		if reset = '1' then
			fifo_inaddr <= (others => '0');
			fifo_outaddr <= (others => '0');
			fifo_count <= (others => '0');
			fifo_overflow <= '0';
			fifo_underrun <= '0';
			fifo_wronly <= '1';
		elsif rising_edge(clk) then
			fifo_out <= fifo_mem(CONV_INTEGER(fifo_outaddr));
			if fifo_ctrl = '1' then
				if fifo_reset = '1' then
					fifo_inaddr  <= (others => '0');
					fifo_outaddr <= (others => '0');
					fifo_count   <= (others => '0');
					fifo_overflow <= '0';
					fifo_underrun <= '0';
					fifo_wronly <= '1';
				end if;
				fifo_wronly <= fifo_wronly_set;
			elsif fifo_we = '1' then
				if fifo_full = '1' then
					fifo_overflow <= '1';
				else
					fifo_mem(CONV_INTEGER(fifo_inaddr)) <= fifo_in;
					fifo_inaddr <= fifo_inaddr + 1;
					fifo_count <= fifo_count + 1;
				end if;
			elsif fifo_next = '1' and fifo_wronly = '0' then
				if fifo_empty = '1' then
					fifo_underrun <= '1';
				else
					fifo_outaddr <= fifo_outaddr + 1;
					fifo_count <= fifo_count - 1;
				end if;
			end if;
		end if;
	end process;
	
end Behavioral;

